#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&После("ТоварыУпаковкаПриИзменении")
&НаКлиенте
Процедура is_ТоварыУпаковкаПриИзменении(Элемент)
	
	// ++
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;

	СтруктураДействий.Вставить("ЗаполнитьВесУпаковки", 
		Новый Структура("Номенклатура, Упаковка", "ВесУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьОбъемУпаковки", 
		Новый Структура("Номенклатура, Упаковка", "ОбъемУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияВеса", 
		Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияВеса"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияОбъема", 
		Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияОбъема"));
	СтруктураДействий.Вставить("ПересчитатьВесОбъем");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено); 
	// --
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
&Перед("ПриЧтенииСозданииНаСервере")
Процедура is_ПриЧтенииСозданииНаСервере()
	
	// ++
	ДобавляемыеРеквизиты = Новый Массив;

	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Вес",	
		Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(15, 3)), "Объект.Товары"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Объем",	
		Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(15, 3)), "Объект.Товары"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ЕдиницаИзмеренияВеса",	
		Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"), "Объект.Товары"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ЕдиницаИзмеренияОбъема",	
		Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"), "Объект.Товары"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВесУпаковки",	
		Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 9)), "Объект.Товары"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОбъемУпаковки",	
		Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 9)), "Объект.Товары"));
		
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	ТоварыГруппаВес = Элементы.Добавить("ТоварыГруппаВес", Тип("ГруппаФормы"), Элементы.Товары);
	ТоварыГруппаВес.Группировка = ГруппировкаКолонок.ВЯчейке;
	
	НовыйЭлемент = Элементы.Добавить("ТоварыВес", Тип("ПолеФормы"), ТоварыГруппаВес);
	НовыйЭлемент.Заголовок = "Вес";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = "Объект.Товары.Вес";
	НовыйЭлемент.Ширина = 8;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "is_ТоварыВесПриИзменении");

	НовыйЭлемент = Элементы.Добавить("ТоварыЕдиницаИзмеренияВеса", Тип("ПолеФормы"), ТоварыГруппаВес);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = "Объект.Товары.ЕдиницаИзмеренияВеса";
	НовыйЭлемент.ОтображатьВШапке = Ложь;
	НовыйЭлемент.Ширина = 4;
	
	ТоварыГруппаОбъем = Элементы.Добавить("ТоварыГруппаОбъем", Тип("ГруппаФормы"), Элементы.Товары);
	ТоварыГруппаОбъем.Группировка = ГруппировкаКолонок.ВЯчейке;
	
	НовыйЭлемент = Элементы.Добавить("ТоварыОбъем", Тип("ПолеФормы"), ТоварыГруппаОбъем);
	НовыйЭлемент.Заголовок = "Объем";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = "Объект.Товары.Объем"; 
	НовыйЭлемент.Ширина = 8;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "is_ТоварыОбъемПриИзменении");
	
	НовыйЭлемент = Элементы.Добавить("ТоварыЕдиницаИзмеренияОбъема", Тип("ПолеФормы"), ТоварыГруппаОбъем);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = "Объект.Товары.ЕдиницаИзмеренияОбъема";
	НовыйЭлемент.ОтображатьВШапке = Ложь;
	НовыйЭлемент.Ширина = 4;
    // --
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
&После("ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок")
Процедура is_ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий, Объект)
	
	// ++
	СтруктураДействий.Вставить("ЗаполнитьВесУпаковки",
		Новый Структура("Номенклатура, Упаковка", "ВесУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьОбъемУпаковки",
		Новый Структура("Номенклатура, Упаковка", "ОбъемУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияВеса",
		Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияВеса"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияОбъема",
		Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияОбъема"));
	СтруктураДействий.Вставить("ПересчитатьВесОбъем");
	// --
	
КонецПроцедуры

&НаСервере
&После("УстановитьУсловноеОформление")
Процедура is_УстановитьУсловноеОформление()
	
	// ++
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыВес.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ВесУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыОбъем.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ОбъемУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЕдиницаИзмеренияВеса.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ВесУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЕдиницаИзмеренияОбъема.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ОбъемУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
    // --
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьСлужебныеРеквизитыПоНоменклатуре")
Процедура is_ЗаполнитьСлужебныеРеквизитыПоНоменклатуре()

	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакТипНоменклатуры",
											Новый Структура("Номенклатура", "ТипНоменклатуры"));
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакАртикул",
											Новый Структура("Номенклатура", "Артикул"));
											
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакВариантОформленияПродажи",
											Новый Структура("Номенклатура", "ВариантОформленияПродажи"));
	
	#Вставка
	// ++
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьВесУпаковки",
		Новый Структура("Номенклатура, Упаковка", "ВесУпаковки"));
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьОбъемУпаковки",
		Новый Структура("Номенклатура, Упаковка", "ОбъемУпаковки"));
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьЕдиницуИзмеренияВеса",
		Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияВеса"));
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьЕдиницуИзмеренияОбъема",
		Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияОбъема"));
	ПараметрыЗаполненияРеквизитов.Вставить("ПересчитатьВесОбъем");
	// --
	#КонецВставки
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары,ПараметрыЗаполненияРеквизитов);

	НаборыСервер.ЗаполнитьСлужебныеРеквизиты(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

